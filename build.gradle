buildscript {
	repositories { jcenter() }

	dependencies {
		classpath group: 'org.github.ngbinh.scalastyle', name: 'gradle-scalastyle-plugin_2.11', version: '0.7.2'
	}
}

plugins { id 'com.github.maiflai.scalatest' version '0.7' }

apply plugin: 'java'
apply plugin: 'scala'
apply plugin: 'eclipse'
apply plugin: 'application'
apply plugin: 'scalaStyle'
apply plugin: 'maven-publish'

repositories {
	mavenLocal()
	mavenCentral()
	maven { url 'http://repo.typesafe.com/typesafe/releases' }
}

version = '0.1'
mainClassName = '' //ugly, but hey, this is a library after all!

ext {
	scala_major_version = '2.11'
	scala_minor_version = '7'
	scala_version = scala_major_version + '.' + scala_minor_version
	scala_suffix = '_' + scala_major_version
}

dependencies {
	//Scala
	compile group: 'org.scala-lang', name: 'scala-compiler', version: scala_version
	compile group: 'org.scala-lang', name: 'scala-library', version: scala_version
	compile group: 'org.scala-lang', name: 'scala-reflect', version: scala_version

	//Persistence
	compile group: 'org.squeryl', name: 'squeryl' + scala_suffix, version: '0.9.5-7'

	//Time
	compile group: 'joda-time', name: 'joda-time', version: '2.5'
	compile group: 'org.joda', name: 'joda-convert', version: '1.7'

	//Test utils
	compile group: 'com.h2database', name: 'h2', version: '1.4.182'
	compile group: 'org.scalatest', name: 'scalatest' + scala_suffix, version: '2.2.5'

	//Testing
	testRuntime group: 'org.pegdown', name: 'pegdown', version:'1.5.0'
	testCompile group: 'org.mockito', name: 'mockito-all', version: '1.+'
}

tasks.withType(ScalaCompile) {
	scalaCompileOptions.useAnt = false
	scalaCompileOptions.useCompileDaemon = true
	scalaCompileOptions.additionalParameters = [
		"-feature",
		"-deprecation",
		"-target:jvm-1.8"
	]
}

test { maxParallelForks = 1 } // FIXME not ideal, but there are thread-unsafe tests

scalaStyle {
	configLocation = "/src/test/resources/scalastyle-config.xml"
	includeTestSourceDirectory = true
	source = "src/main/scala"
	testSource = "src/test/scala"
}

task sourceJar(type: Jar) {
	from sourceSets.main.allJava
	from sourceSets.main.allScala
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			groupId 'org.chipmunk'
			artifactId 'chipmunk'

			from components.java

			artifact sourceJar { classifier "sources" }
		}
	}
}